;------------------------------------------------------------------------------
;
; Copyright (c) 2006 - 2012, Intel Corporation. All rights reserved.<BR>
; Copyright (c) 2017, AMD Incorporated. All rights reserved.<BR>
;
; This program and the accompanying materials are licensed and made available
; under the terms and conditions of the BSD License which accompanies this
; distribution.  The full text of the license may be found at
; http:
;
; THE PROGRAM IS DISTRIBUTED UNDER THE BSD LICENSE ON AN "AS IS" BASIS,
; WITHOUT WARRANTIES OR REPRESENTATIONS OF ANY KIND, EITHER EXPRESS OR IMPLIED.
;
;------------------------------------------------------------------------------

    SECTION .text

;------------------------------------------------------------------------------
;  void
;  __cdecl
;  IoReadFifo8 (
;      UINTN                 Port,
;      UINTN                 Size,
;     void                  *Buffer
;    );
;------------------------------------------------------------------------------
global _IoReadFifo8
_IoReadFifo8:
    push    edi
    cld
    mov     dx, [esp + 8]
    mov     ecx, [esp + 12]
    mov     edi, [esp + 16]
rep insb
    pop     edi
    ret

;------------------------------------------------------------------------------
;  void
;  __cdecl
;  IoReadFifo16 (
;      UINTN                 Port,
;      UINTN                 Size,
;     void                  *Buffer
;    );
;------------------------------------------------------------------------------
global _IoReadFifo16
_IoReadFifo16:
    push    edi
    cld
    mov     dx, [esp + 8]
    mov     ecx, [esp + 12]
    mov     edi, [esp + 16]
rep insw
    pop     edi
    ret

;------------------------------------------------------------------------------
;  void
;  __cdecl
;  IoReadFifo32 (
;      UINTN                 Port,
;      UINTN                 Size,
;     void                  *Buffer
;    );
;------------------------------------------------------------------------------
global _IoReadFifo32
_IoReadFifo32:
    push    edi
    cld
    mov     dx, [esp + 8]
    mov     ecx, [esp + 12]
    mov     edi, [esp + 16]
rep insd
    pop     edi
    ret

;------------------------------------------------------------------------------
;  void
;  __cdecl
;  IoWriteFifo8 (
;     UINTN                  Port,
;     UINTN                  Size,
;     void                   *Buffer
;    );
;------------------------------------------------------------------------------
global _IoWriteFifo8
_IoWriteFifo8:
    push    esi
    cld
    mov     dx, [esp + 8]
    mov     ecx, [esp + 12]
    mov     esi, [esp + 16]
rep outsb
    pop     esi
    ret

;------------------------------------------------------------------------------
;  void
;  __cdecl
;  IoWriteFifo16 (
;     UINTN                  Port,
;     UINTN                  Size,
;     void                   *Buffer
;    );
;------------------------------------------------------------------------------
global _IoWriteFifo16
_IoWriteFifo16:
    push    esi
    cld
    mov     dx, [esp + 8]
    mov     ecx, [esp + 12]
    mov     esi, [esp + 16]
rep outsw
    pop     esi
    ret

;------------------------------------------------------------------------------
;  void
;  __cdecl
;  IoWriteFifo32 (
;     UINTN                  Port,
;     UINTN                  Size,
;     void                   *Buffer
;    );
;------------------------------------------------------------------------------
global _IoWriteFifo32
_IoWriteFifo32:
    push    esi
    cld
    mov     dx, [esp + 8]
    mov     ecx, [esp + 12]
    mov     esi, [esp + 16]
rep outsd
    pop     esi
    ret

